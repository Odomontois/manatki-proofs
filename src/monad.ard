\import Function (o)
\import Paths
\import list
\import monoid
\import funs
\import sets

\record TypeConstr (F : \Set -> \Set)

\class Functor \extends TypeConstr {
  \field map : \Pi {A B : \Set} ->  (A -> B)  -> F A -> F B
  \property identity : \Pi { A : \Set } (fa : F A) -> map id fa  = fa
  \property composition : \Pi { A B C : \Set } (fa : F A) (f : A -> B) (g : B -> C) -> map (g o f) fa  = (map g o map f) fa
}

\record BaseMonad \extends TypeConstr {
  | pure : \Pi {A : \Set } -> A -> F A
  | flatMap : \Pi {A B : \Set  } -> F (A) -> (A -> F B) -> F B
  \property left_unit : \Pi { A B : \Set  } (a : A) (f : A -> F B) -> flatMap (pure a) f = f a
  \property right_unit : \Pi { A : \Set  } (fa : F A) -> flatMap fa pure = fa
  \property assocmonad : \Pi { A B C : \Set} (fa : F A) (f : A -> F B) (g : B -> F C) ->
      flatMap fa (\lam x => flatMap (f x) g) = flatMap ( flatMap fa f ) g
}

\class Monad \extends BaseMonad , Functor {
  | map f fa  => flatMap fa (\lam x => pure (f x))
  | identity => right_unit
  | composition {_} {_} {C}  fa f g =>
    \let | gp x => pure (g x)
         | lu_extended => funExt (\lam _ => F C) gp (\lam b => flatMap (pure b) gp) (\lam b => inv ( left_unit b gp))
    \in pmap (\lam u => flatMap fa (u ~ f)) lu_extended *>  assocmonad fa (pure ~ f) gp
}

\record Trans {F : \Set -> \Set} {G : \Set -> \Set} (t : \Pi {A : \Set} (F A) -> G A)

\record  NatTrans \extends Trans   {
  \field ffunc : Functor F
  \field gfunc : Functor G
  \property naturality {A B : \Type} (f : A -> B) (fa : F A) : (gfunc.map f o t) fa  = (t o ffunc.map f) fa
}

\record  MonadTransBase \extends Trans{
  \field fmon : Monad F
  \field gmon : Monad G
  \property purity {A : \Type} (a : A) : gmon.pure a = (t o fmon.pure) a
  \property flatness {A B : \Type} (fa : F A) (f : A -> F B) : gmon.flatMap (t fa) (t o f) = t (fmon.flatMap fa f)
}

\record MonadTrans \extends NatTrans, MonadTransBase{
  | ffunc => fmon
  | gfunc => gmon
  | naturality f fa => path (\lam i => gmon.flatMap (t fa) (\lam a => purity (f a) @ i  )) *> flatness fa (fmon.pure o f)
}
