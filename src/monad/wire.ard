\import Paths
\import monad
\import monoid
\open Semigroup (|+|)
\open Monoid (emp)

\data Socket (S : \Type) (A : \Type)
  | vacant S
  | full  S A
  \where {
    \func addL {S A : \Type} {sg : Semigroup S} (s1 : S) (sock : Socket S A) : Socket S A =>
      \case sock \with {
        | vacant s2 => vacant (s1 |+| s2)
        | full s2 a => full (s1 |+| s2) a
      }

    \func addLlu {S A : \Type} {m : Monoid S} (sock : Socket S A) : addL emp sock = sock \elim sock
      | vacant s => pmap vacant (m.left_unit s)
      | full s a => pmap (\lam x => full x a) (m.left_unit s)
  }

\func socketMonad (S : \Set) {smon : Monoid S} : Monad (Socket S) \cowith
  | pure a => full emp a
  | flatMap sa => flmap sa
  | left_unit a f => Socket.addLlu (f a)
  | right_unit sa => runit sa
  | associativity {A} fa f g =>n
    \let eqfa fa' => flmap fa' (\lam x => flmap (f x) g) = flmap (flmap fa' f) g
    \in \case fa \as sock, idp : sock = fa \with {
      | vacant s , efa => transport eqfa efa idp
      | full s a , efa =>
        \let | eqf f_a' => Socket.addL s (flmap f_a' g) = flmap (Socket.addL s f_a') g
             | H1 : eqf (f a) => \case f a \as f_a, idp : f_a = f a \with {
               | vacant s1, ef_a => transport eqf ef_a idp
               | full s1 b, ef_a =>
                 \let | eqg g_b => Socket.addL s (Socket.addL s1 g_b) = Socket.addL (s |+| s1) g_b
                      | H2 : eqg (g b) => \case g b \as g_b, idp : g_b = g b \with {
                        | vacant s2 , eg_b => transport eqg eg_b (pmap vacant (inv (smon.associativity s s1 s2)))
                        | full s2 c, eg_b => transport eqg eg_b (pmap (\lam s' => full s' c) (inv (smon.associativity s s1 s2)))
                      }
                 \in transport eqf ef_a H2
             }
        \in  transport eqfa efa H1
    }
  \where {
    \func flmap {S A B : \Type}  {smon : Monoid S} (sa : Socket S A) (f : A -> Socket S B) : Socket S B \elim sa
      | vacant s => vacant s
      | full s a => Socket.addL s (f a)

    \func runit {S A : \Type} {smon : Monoid S} (sa : Socket S A) : flmap sa (full emp) = sa \elim sa
      | vacant s => idp
      | full s a => pmap (\lam x => full x a) (smon.right_unit s)
  }

\func wireMonad (S : \Set) : Monad (\lam A => S -> Socket S A) \cowith
  | pure a s => full s a
  | flatMap wa fab s => cont (wa s) fab
  | left_unit a f => idp
  | right_unit {A} wa => path (\lam i s => runit (wa s) @ i)
  | associativity {A} {B} {C} wa f g => path ( \lam i s => assoc (wa s) f g @ i )
  \where {
    \func runit {S A : \Set} (sa : Socket S A): cont sa pur = sa \elim sa
      | vacant _ => idp
      | full _ _ => idp

    \func assoc {S A B C : \Type} (sa : Socket S A) (f : A -> S -> Socket S B) (g : B -> S -> Socket S C) :
    cont sa (\lam x s => cont (f x s) g) = cont (cont sa f) g \elim sa
      | vacant _ => idp
      | full _ _ => idp

    \func pur {S A : \Type } (a : A) (s : S) : Socket S A => full s a

    \func cont {S A B : \Type} (was : Socket S A) (fab : A -> S -> Socket S B)  : Socket S B => \case was \with{
      | vacant s1 => vacant s1
      | full s1 a => fab a s1
    }
  }

\func wireTMonad {S : \Set} {X : \Set -> \Set} (fm : Monad X) : Monad (\lam A => S -> X (Socket S A)) \cowith
  | pure x s => fm.pure (full s x)
  | flatMap wa => flmap X fm wa
  | left_unit {_} {B} a fab => path (\lam i s => fm.left_unit (full s a) (flmatch X fm fab) @ i)
  | right_unit {A} wa => path (\lam i s  => right-unit-step X {fm} wa s @ i )
  | associativity {A} {B} {C} wa f g => path (\lam i s => assoc-1 X fm wa f g s @ i)
  \where {
    \lemma assoc-1 {A B C S : \Set} (X : \Set -> \Set) (fm : Monad X) (wa : S -> X ( Socket S A))
                   (f : A -> S -> X (Socket S B)) (g : B -> S -> X (Socket S C)) (s : S):
    fm.flatMap (wa s) (flmatch X fm (\lam b s1 => fm.flatMap (f b s1) (flmatch X fm g))) =
    fm.flatMap (fm.flatMap (wa s) (flmatch X fm f)) (flmatch X fm g) =>
      transport (\lam u => fm.flatMap (wa s) u = fm.flatMap (fm.flatMap (wa s) (flmatch X fm f)) (flmatch X fm g))
                (path ( \lam i s => assoc-2 X fm f g s @ i)) (fm.associativity (wa s) (_) (_))

    \lemma assoc-2 {A B C S : \Set} (X : \Set -> \Set) (fm : Monad X)
                   (f : A -> S -> X (Socket S B)) (g : B -> S -> X (Socket S C)) (sa : Socket S A) :
    fm.flatMap (flmatch X fm f sa) (flmatch X fm g) =
    flmatch X fm (\lam b s1 => fm.flatMap (f b s1) (flmatch X fm g)) sa \elim sa
      | vacant s => fm.left_unit (vacant s) (flmatch X fm g)
      | full s a => idp

    \lemma right-unit-step {S A : \Set} (X : \Set -> \Set) {fm : Monad X} (wa : S -> X (Socket S A)) (s : S) :
    flmap X fm wa (\lam x s => fm.pure (full s x)) s = wa s =>
      \let | r2 : fm.flatMap (wa s) (fm.pure) = wa s => fm.right_unit (wa s)
      \in transport (\lam x => fm.flatMap (wa s) x = wa s) (inv (flmatch-pure-full fm)) r2

    \func pure-full {S A : \Set} (X : \Set -> \Set) (fm : Monad X) (a : A) (s : S) : X (Socket S A) =>
      fm.pure (full s a)

    \func flmap {S A B : \Set} (X : \Set -> \Set) (fm : Monad X) (wa : S -> X( Socket S A)) (fab : A -> S -> X (Socket S B)) (s : S) : X (Socket S B) =>
      fm.flatMap (wa s) ( flmatch X fm fab )

    \func flmatch {S A B : \Set} (X : \Set -> \Set) (fm : Monad X) (fab : A -> S -> X (Socket S B)) (sa : Socket S A) : X (Socket S B) \elim sa
      | vacant s1 => fm.pure( vacant s1 )
      | full s1 a => fab a s1

    \lemma flmatch-pure-full-s {S A : \Set} {X : \Set -> \Set} (fm : Monad X) (sa : Socket S A) :
    flmatch {S} {A} {A} X fm (pure-full X fm) sa = fm.pure sa \elim sa
      | vacant _ => idp
      | full _ _ => idp

    \lemma flmatch-pure-full {S A : \Set} {X : \Set -> \Set} (fm : Monad X) :
    flmatch {S} {A} {A} X fm (pure-full X fm) = fm.pure =>
      funExt (\lam _ => X( Socket S A)) (_) fm.pure (flmatch-pure-full-s fm)
  }

