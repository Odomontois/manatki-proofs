\import DecEq
\import HLevel
\import Logic
\import Logic
\import Logic.Meta
\import Paths
\import Paths.Meta
\import Set.Fin
\import funs
\import list.symmetry
\import monoid

--\import Algebra.Monoid (+)
--\import order

\import Function
\import Order.LinearOrder
\import Order.PartialOrder
\import Order.Lattice
\import Set

\data List (A : \Type)
  | nil
  | \infixr 6 :: (a : A) (tail : List A) \where {
  \cons single {A : \Type} (a : A) : List A => a :: nil

  \func \infix 10 ## {A B : \Type} (f : List A -> B) (a : A) => f (single a)

  \lemma discriminate {A : \Type} {a : A} {t : List A} (p : nil = a :: t) : Empty => contradiction

  \lemma head-monotone {A : \Set} {la : Poset A} (a b : A) (as : List A) (p : a <= b) : head a as <= head b as \elim as
    | nil => p
    | :: c _ => <=-reflexive

  \func head {A : \Type} (a : A) (l : List A) : A \elim l
    | nil => a
    | :: b _ => b

  \func at {A : \Type} (l : List A) (n : Fin (length l)) : A \elim l,n
    | :: a tail, fzero => a
    | :: a tail, fsuc n => at tail n

  \func tail {A : \Type} (l : List A) : List A \elim l
    | nil => nil
    | :: _ t => t

  \lemma ::-injective {A : \Set} {a a' : A} {t  t' : List A} (p : a :: t = a' :: t') : \Sigma (a = a') (t = t') =>
    ( pmap (head a) p , pmap tail p )

  \func length {A : \Type} (l : List A) : Nat
    | nil => Nat.zero
    | :: _ t => suc (length t)

  \func \infixr 5 ++ {A : \Type} (l1 : List A) (l2 : List A) : List A \elim l1
    | nil => l2
    | (:: h t) => h :: (t ++ l2)

  \func ++nil {A : \Type} (l : List A): l ++ nil = l
    | nil => idp
    | (:: h t) => pmap (:: h) (++nil t)

  \func assoc {A : \Type} (la lb lc : List A) : (la ++ lb) ++ lc = la ++ (lb ++ lc)  \elim la
    | nil => idp
    | (:: h la') => pmap (:: h) (assoc la' lb lc)

  \open TotalOrder (totality)
  \open || (byLeft, byRight)
  \open JoinSemilattice (join-comm)

  \func minimum {A : \Set} {sa : JoinSemilattice A} (a : A) (as : List A) : A \elim as
    | nil => a
    | :: a' tail => minimum (join a a') tail
    \where {
      \lemma unordered {A : \Set} {to : JoinSemilattice A} (a a' : A) (as : List A) :
        minimum a (a' :: as) = minimum a' (a :: as) => pmap (\lam x => minimum x as) join-comm
    }

  \func reverse {A : \Type} (l : List A) : List A
    | nil => nil
    | :: h t => reverse t ++ (h :: nil) \where {
    \func antisym {A : \Type} (l1 l2 : List A) : reverse (l1 ++ l2) = reverse l2 ++ reverse l1
      | nil, _ => inv ( ++nil _)
      | :: a tail, nil => pmap (\lam x => reverse x ++ (a :: nil)) (++nil _)
      | :: a tail, :: a1 tail1 => pmap (\lam x => x ++ (a :: nil)) (antisym _ _) *> assoc _ _ _

    \func involutive {A : \Type} (l : List A) : reverse (reverse l) = l
      | nil => idp
      | :: h t => antisym _ _ *> pmap (:: h) (involutive t)
  }

  \func reverse' {A : \Type} (l : List A) : List A => step l nil \where {
    \func step {A : \Type} (l acc : List A) : List A \elim l
      | nil => acc
      | :: h t => step t (h :: acc) \where {
      \func cons {A : \Type} (l acc l' : List A) : step l (acc ++ l') = step l acc ++ l' \elim  l
        | nil => idp
        | :: a tail => cons _ _ _

      \func inv {A : \Type} (l acc : List A) : reverse' (step l acc) = step acc l \elim l
        | nil => idp
        | :: a tail => inv _ _

      \func comp {A : \Type} (l l' acc : List A) : step (l ++ l') acc = step l' (step l acc) \elim l
        | nil => idp
        | :: a tail => comp _ _ _
    }

    \func antisym {A : \Type} (l l' : List A) : reverse' (l ++ l') = reverse' l' ++ reverse' l =>
      step.comp _ _ _ *> step.cons _ _ _

    \func involutive {A : \Type} (l : List A) : reverse' (reverse' l) = l => step.inv l nil
  }
}

\instance listMonoid {A : \Set}: Monoid (List A)
  | empty => nil
  | |+| => List.++
  | left_empty a => idp
  | right_empty => List.++nil
  | assoc  => List.assoc

\instance listDecEq {A : DecSet}: DecSet.Impl (List A)
  | decideEq => impl \where {
  \lemma impl {A : DecSet} (a b : List A) : Dec (a = b) \elim a, b
    | nil, nil => yes (idp)
    | nil, :: _ _ => no List.discriminate
    | :: _ _, nil => no (List.discriminate o inv)
    | :: ha ta, :: hb tb => \case ha `decideEq` hb \with {
      | no n => no (\lam e => n (pmap (List.head ha) e))
      | yes eh => \case impl ta tb \with {
        | no n => no (\lam e => n (pmap List.tail e))
        | yes et => yes (pmap2 (::) eh et)
      }
    }
}

\data Index {A : \Type} (e : A) (l : List A) \elim l
  | :: h t => Here (e = h)
  | :: h t => There (Index e t) \where {
  \open List (++)

  \func at {A : \Type} {l : List A} (n : Fin (List.length l)) : Index (List.at l n) l \elim l,n
    | :: a tail, fzero => Here idp
    | :: a tail, fsuc n => There (at n)

  \func swap-head {A : \Type} {a b e : A} {l : List A} (i : Index e (a :: b :: l)) : Index e (b :: a :: l)
    | Here p => There (Here p)
    | There (Here p) => Here p
    | There (There i) => There (There i)

  \func swap {A : \Type} {a b e : A} {l l' : List A} (i : Index e (l' ++ a :: b :: l)) : Index e (l' ++ b :: a :: l) \elim l', i
    | nil, i => swap-head i
    | :: c l', Here p => Here p
    | :: c l', There i => There (swap i)

  \func index-sym {A : \Type} {e : A} : Symmetry (Index e) \cowith
    | swap p => swap p

  \func no-nil {A B : \Type} (a : A) (eq : Index a nil) : B

  \func here!=there {A B : \Type} {e h : A} {t : List A}
                    {el : Index e t} {eq : e = h}
                    (p : Here eq = There el)  : B =>
    coe (\lam i => \case p @ i \with {
    Here _ => \Sigma | There _ => B
    }) () right

  \func there-injective {A : \Type} {a h : A} {l : List A}
                        {e1 e2 : Index a l} (p : There {_} {_} {h} e1 = There e2) : e1 = e2 =>
    \let f (e : Index a (h :: l)) : Index a l => \case e \with {
      | Here _ => e1
      | There e' => e'
    }
    \in pmap f p

  \func @0 {A : \Type} {a : A} {l : List A} : Index a (a :: l) => Here idp

  \func @1 {A : \Type} {a b : A} {l : List A}: Index b (a :: b :: l) => at (fnat 1)

  \func @2 {A : \Type} {a b c : A} {l : List A}: Index c (a :: b :: c :: l) => at (fnat 2)

  \func squash-0 {A : \Type} {e e' : A} {l : List A} (p1 : Index e l) (p2 : Index e' (e :: l)) : Index e' l \elim l, p1, p2
    | :: a l, Here p1, Here p2 => Here $ p2 *> p1
    | :: a l, _, There p2 => p2
    | :: a l, There i1, Here p2 => There $ transportInv (Index __ l) p2 i1

  \func squash {A : \Type} {e e' : A} {l l' : List A} (p1 : Index e l) (p2 : Index e' (l' ++ e :: l)) : Index e' (l' ++ l) \elim l', p2
    | nil, p2 => squash-0 p1 p2
    | :: a l', Here p2 => Here p2
    | :: a l', There p2 => There $ squash p1 p2
}

\instance elemDecEq {A : DecSet} {e : A} {l : List A} : DecSet.Impl (Index e l)
  | decideEq => impl \where {
  \lemma impl {A : DecSet} {e : A} {l : List A} (a b : Index e l) : Dec (a = b) \elim l, a, b
    | :: a tail, Here p1, Here p2 => yes (pmap Here (Path.inProp {e = a} p1 p2))
    | :: a tail, Here =, There e => no Index.here!=there
    | :: a tail, There e, Here = => no (Index.here!=there o inv)
    | :: a tail, There e1, There e2 => \case impl e1 e2 \with {
      | yes p => yes (pmap There p)
      | no n => no (n  o Index.there-injective)
    }
}

\func Elem {A : \Type} (e : A) (l : List A) => TruncP (Index e l) \where {
  \open List (++)

  \lemma hereidp {A : \Type} {a : A} {l : List A} : Elem a (a :: l) => here idp

  \lemma @0 {A : \Type} {a : A} {l : List A} : Elem a (a :: l) => here idp

  \lemma @1 {A : \Type} {a b : A} {l : List A}: Elem b (a :: b :: l) => at (fnat 1)

  \lemma @2 {A : \Type} {a b c : A} {l : List A}: Elem c (a :: b :: c :: l) => at (fnat 2)

  \func at {A : \Type} {l : List A} (n : Fin (List.length l)) : Elem (List.at l n) l => TruncP.inP (Index.at n)

  \cons here {A : \Type} {a h : A} {l : List A} (p : a = h) : Elem a (h :: l) => TruncP.inP (Index.Here p)

  \cons there {A : \Type} {a h : A} {l : List A} (e : Index a l) : Elem a (h :: l) => TruncP.inP (Index.There e)

  \lemma there' {A : \Type} {a h : A} {l : List A} (e : Elem a l) : Elem a (h :: l) \elim e
    | inP i => TruncP.inP (Index.There i)

  \func elemSym {A : \Type} {e : A} : Symmetry (Elem e) \cowith
    | swap p => \case p \with {
      | inP idx => inP (Index.swap idx)
    }

  \lemma swap {A : \Type} {a b e : A} {l l' : List A} (i : Elem e (l' ++ a :: b :: l)) : Elem e (l' ++ b :: a :: l)
    | inP idx => inP (Index.swap idx)

  \lemma squash {A : \Type} {e e' : A} {l : List A} (p1 : Elem e l) (p2 : Elem e' (e :: l)) : Elem e' l \elim l, p1, p2
    | :: a l, here p1, here p2 => here $ p2 *> p1
    | :: a l, _, there p2 => inP p2
    | :: a l, there i1, here p2 => there $ transportInv (Index __ l) p2 i1
    | nil, inP p1, p2 => contradiction
}

\open Nat (+)

\func fnat {n : Nat} (m : Nat): Fin (suc (n + m)) \elim m
  | 0 => fzero
  | suc m => fsuc (fnat m)






